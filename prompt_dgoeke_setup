function EXT_COLOR () { echo -ne "\e[38;5;$1m"; }
function CLOSE_COLOR () { echo -ne '\e[m'; }

function prompt_dgoeke_precmd {
  git-info
  prompt_dgoeke_pwd
}

function prompt_dgoeke_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_dgoeke_pwd="$MATCH"
    unset MATCH
  else
    _prompt_dgoeke_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_dgoeke_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_dgoeke_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' (%f$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s)%f' \
    'rprompt' '%C%D'

  # Define prompts.
  PROMPT='%F{187}${_prompt_dgoeke_pwd}%f${(e)git_info[prompt]}%p $ '
  RPROMPT=''
}

prompt_dgoeke_setup "$@"

